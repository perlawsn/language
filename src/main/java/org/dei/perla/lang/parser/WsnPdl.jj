/* Copyright (C) 2008  Romolo Camplani, Marco Fortunato, Marco Marelli, Guido Rota, Fabio A. Schreiber et al.
 *
 * This program is free software; you can redistribute it and/or
 * modify it under the terms of the GNU General Public License
 * as published by the Free Software Foundation; either version 2
 * of the License, or (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA  02110-1301, USA.
 */
options
{
  JDK_VERSION = "1.8";
  IGNORE_CASE = true;
}

PARSER_BEGIN(Parser)

	public class Parser {

	}

PARSER_END(Parser)

/*
 * SEPARATORS
 */
SKIP :
{
 	" "
|
	"\r"
|
	"\t"
|
	"\n"
}

/*
 *	COMMENTS
 */
MORE:
{
	"/*" : COMMENT
}

<COMMENT> MORE:
{
	<~[]>
}

<COMMENT> SKIP:
{
	"*/" : DEFAULT
}

/*
 * CREATE CLAUSE
 */
TOKEN :
{
	< KEYWORD_CREATE:   "CREATE" >
    |
	< KEYWORD_AS:       "AS"     >
}

/*
 * INSERT CLAUSE
 */
TOKEN :
{
	< KEYWORD_INSERT:   "INSERT" >
    |
	< KEYWORD_INTO:     "INTO" >
}

/*
 * EVERY CLAUSE
 */
TOKEN :
{
	< KEYWORD_EVERY:        "EVERY"        >
	|
	< KEYWORD_SYNCHRONIZED: "SYNCHRONIZED" >
	|
	< KEYWORD_IN:           "IN"           >
}

/*
 * SELECT CLAUSE
 */
TOKEN :
{
	< KEYWORD_SELECT:    "SELECT"   >
    |
	< KEYWORD_DISTINCT:  "DISTINCT" >
    |
	< KEYWORD_ALL:       "ALL"      >
    |
	// Virtual record field created by the GROUP BY clause
	<KEYWORD_GROUPTS:    "GROUP_TS" >
}

/*
 * UNION CLAUSE
 */
TOKEN :
{
	< KEYWORD_UNION:     "UNION" >
//  |
//	< KEYWORD_ALL:       "ALL"   >
}

/*
 * WHERE CLAUSE  */
TOKEN :
{
	< KEYWORD_WHERE:     "WHERE" >
}

/*
 * GROUP BY CLAUSE
 */
TOKEN :
{
	< KEYWORD_GROUP:     "GROUP"  >
    |
	< KEYWORD_BY:        "BY"     >
    |
    // Keyword employed to define the number of groups created by a timestamp
    // aggregation
	< KEYWORD_GROUPS:    "GROUPS" >
}

/*
 * HAVING CLAUSE
 */
TOKEN :
{
	< KEYWORD_HAVING:    "HAVING" >
}

/*
 * UP TO CLAUSE
 */
TOKEN :
{
	< KEYWORD_UP:        "UP" >
    |
	< KEYWORD_TO:        "TO" >
}

/*
 * ON EMPTY SELECTION CLAUSE
 */
TOKEN :
{
	< KEYWORD_ON:        "ON"        >
    |
	< KEYWORD_EMPTY:     "EMPTY"     >
    |
	< KEYWORD_SELECTION: "SELECTION" >
    |
//	< KEYWORD_INSERT:    "INSERT"    >
//  |
	< KEYWORD_NOTHING:   "NOTHING"   >
    |
	< KEYWORD_DEFAULT:   "DEFAULT"   >
}

/*
 * SAMPLING CLAUSE
 */
TOKEN :
{
	< KEYWORD_SAMPLING:  "SAMPLING" >
    |
//	< KEYWORD_ON:        "ON"       >
//  |
	< KEYWORD_EVENT:     "EVENT"    >
    |
//	< KEYWORD_IF:        "IF"       >
//  |
//	< KEYWORD_EVERY:     "EVERY"    >
//  |
	< KEYWORD_ELSE:      "ELSE"     >
}

/*
 *	Token della clausola *ON UNSUPPORTED SAMPLING RATE*
 */
TOKEN :
{
//	< KEYWORD_ON:          "ON"          >
//  |
	< KEYWORD_UNSUPPORTED: "UNSUPPORTED" >
    |
	< KEYWORD_SAMPLE:      "SAMPLE"      >
    |
	< KEYWORD_RATE:        "RATE"        >
    |
	< KEYWORD_DO:          "DO"          >
    |
//	< KEYWORD_NOT:         "NOT"         >
//  |
	< KEYWORD_SLOW:        "SLOW"        >
    |
	< KEYWORD_DOWN:        "DOWN"        >
}

/*
 * REFRESH CLAUSE
 */
TOKEN :
{
	< KEYWORD_REFRESH:   "REFRESH" >
    |
//	< KEYWORD_EVERY:     "EVERY"   >
//  |
	< KEYWORD_NEVER:     "NEVER"   >
}

/*
 * EXECUTE IF CLAUSE
 */
TOKEN :
{
	< KEYWORD_EXECUTE:   "EXECUTE" >
    |
	< KEYWORD_IF:        "IF"      >
    |
	< KEYWORD_EXISTS :   "EXISTS" >
}

/*
 * TERMINATE AFTER CLAUSE
 */
TOKEN :
{
	< KEYWORD_TERMINATE:  "TERMINATE"  >
    |
	< KEYWORD_AFTER:      "AFTER"      >
    |
	< KEYWORD_SELECTIONS: "SELECTIONS" >
}

/*
 * FROM CLAUSE
 */
TOKEN :
{
	< KEYWORD_FROM:      "FROM" >
//  |
//	< KEYWORD_AS:        "AS"   >
}

/*
 * SET CLAUSE
 */
TOKEN :
{
  	< KEYWORD_SET:    "SET"  >
//  |
//  < KEYWORD_ON: "ON"    >
}

/*
 * STREAM DATA STRUCTURE
 */
TOKEN :
{
	< KEYWORD_STREAM:   "STREAM"   >
}

/*
 * MISC TOKENS
 */
TOKEN :
{
	< KEYWORD_SAMPLES:  "SAMPLES"  >
    |
	< KEYWORD_ONE:      "ONE"      >
    |
	< KEYWORD_IS:       "IS" >
    |
	< KEYWORD_BETWEEN:  "BETWEEN"  >
    |
	< KEYWORD_LIKE:     "LIKE"     >
}

/*
 * ALGEBRAIC OPERATORS
 */
TOKEN :
{
	< OPERATOR_MULTIPLY: "*" >
    |
	< OPERATOR_DIVIDE:   "/" >
    |
	< OPERATOR_PLUS:     "+" >
    |
	< OPERATOR_MINUS:    "-" >
}

/*
 * BOOLEAN OPERATORS
 */
TOKEN :
{
	< OPERATOR_NOT:     "NOT" >
    |
	< OPERATOR_XOR:     "XOR" >
    |
	< OPERATOR_AND:     "AND" >
    |
    < OPERATOR_OR:      "OR"  >
}

/*
 * BITWISE OPERATORS
 */
TOKEN :
{
	< OPERATOR_BITWISE_NOT: "!" >
    |
	< OPERATOR_BITWISE_LSH: "<<" >
    |
	< OPERATOR_BITWISE_RSH: ">>" >
    |
    < OPERATOR_BITWISE_XOR: "^" >
    |
	< OPERATOR_BITWISE_AND: "&" >
    |
	< OPERATOR_BITWISE_OR:  "|"  >
}

/*
 * COMPARISON OPERATORS
 */
TOKEN :
{
	< OPERATOR_GREATER:       ">"  >
    |
	< OPERATOR_LESS:          "<"  >
    |
	< OPERATOR_GREATER_EQUAL: ">=" >
    |
	< OPERATOR_LESS_EQUAL:    "<=" >
    |
	< OPERATOR_EQUAL:         "="  >
    |
	< OPERATOR_NOT_EQUAL:     "<>" | "!=" >
}

/*
 *	FUNZIONI DI AGGREGAZIONE
 */
TOKEN :
{
	< FUNCTION_COUNT:     "COUNT"     >
    |
	< FUNCTION_AVG:       "AVG"       >
    |
	< FUNCTION_MAX:       "MAX"       >
    |
	< FUNCTION_MIN:       "MIN"       >
    |
	< FUNCTION_SUM:       "SUM"       >
//  |
//	< FUNCTION_TIMESTAMP: "TIMESTAMP" >
}

/*
 * TIME UNITS
 */
TOKEN :
{
	< TIMEUNIT_S:  "seconds"      | "s"  >
    |
	< TIMEUNIT_M:  "minutes"      | "m"  >
    |
	< TIMEUNIT_H:  "hours"        | "h"  >
    |
	< TIMEUNIT_MS: "milliseconds" | "ms" >
    |
	< TIMEUNIT_D:  "days"         | "d"  >
    |
	< TIMEUNIT_MT: "months"       | "mt" >
}

/*
 * DATA TYPES
 */
TOKEN :
{
	< TYPE_ID:      "ID"        >
    |
	< TYPE_TS:      "TIMESTAMP" >
    |
	< TYPE_BOOLEAN: "BOOLEAN"   >
    |
	< TYPE_INTEGER: "INTEGER"   >
    |
	< TYPE_FLOAT:   "FLOAT"     >
    |
	< TYPE_STRING:  "STRING"    >
}

/*
 * COSTANTS
 * Don't use these tokens, use the corresponding productions instead.
 */

/*
 * CONSTANT NULL
 */
TOKEN :
{
	< CONSTANT_NULL:     "NULL" >
}

/*
 * 3-VALUE BOOLEAN LOGIC (true, false, unknown)
 */
TOKEN :
{
	< CONSTANT_BOOLEAN_TRUE:    "TRUE"    >
    |
	< CONSTANT_BOOLEAN_FALSE:   "FALSE"   >
    |
	< CONSTANT_BOOLEAN_UNKNOWN: "UNKNOWN" >
}

/*
 * NUMERIC CONSTANTS (float and integer)
 */
TOKEN :
{
	<CONSTANT_INTEGER:
		(
		( <DIGIT> )+
		|
		<HEXADECIMAL>
		)
	>
    |
	<CONSTANT_FLOAT:
		(
			( <CONSTANT_INTEGER> )? "." <CONSTANT_INTEGER> ( "E" (<OPERATOR_PLUS> | <OPERATOR_MINUS>)? <CONSTANT_INTEGER> )?
		|
			 <CONSTANT_INTEGER> "E" (<OPERATOR_PLUS> | <OPERATOR_MINUS>)? <CONSTANT_INTEGER>
		)
	>
}

/*
 * STRING CONSTANTS
 * Strings can be single quoted or double quoted. The quote character is
 * escaped when doubled (e.g., 'test '' string' or "test "" string"
 */
TOKEN :
{
	< CONSTANT_SINGLE_QUOTED_STRING_START: "'"  > : NON_SINGLE_QUOTED_STRING
    |
	< CONSTANT_DOUBLE_QUOTED_STRING_START: "\"" > : NON_DOUBLE_QUOTED_STRING
}

<NON_SINGLE_QUOTED_STRING> TOKEN :
{
	< CONSTANT_SINGLE_QUOTED_STRING_VALUE: (~["'"] | "'" "'")* > : NON_SINGLE_QUOTED_STRING_END
}

<NON_DOUBLE_QUOTED_STRING> TOKEN :
{
	< CONSTANT_DOUBLE_QUOTED_STRING_VALUE: (~["\""] | "\"" "\"")* > : NON_DOUBLE_QUOTED_STRING_END
}

<NON_SINGLE_QUOTED_STRING_END> TOKEN :
{
	< CONSTANT_SINGLE_QUOTED_STRING_END:  "'" > : DEFAULT
}

<NON_DOUBLE_QUOTED_STRING_END> TOKEN :
{
	< CONSTANT_DOUBLE_QUOTED_STRING_END: "\"" > : DEFAULT
}

/*
 * IDENTIFIERS
 */
TOKEN :
{
	< #DIGIT:      ["0" - "9"]               >
    |
	< #LITERAL:    ["a" - "z", "A" - "Z"] >
    |
	< #UNDERSCORE: "_"                       >
    |
	< #HEXADECIMAL: "0" ["x","X"] (["0"-"9","a"-"f","A"-"F"])+ >
    |
	< IDENTIFIER: ( <LITERAL> | <UNDERSCORE>) ( <DIGIT> | <LITERAL> | <UNDERSCORE> )* >
}

/*
 * PRODUCTIONS
 */

void StatementSequence() :
{}
{
	Statement()
	(
		";"
		Statement()
	)*
	<EOF>
}

void Statement():
{}
{
    SetParametersStament()
    |
    CreationStatement()
    |
    InsertionStatement()
}

void SetParametersStament():
{}
{
    <KEYWORD_SET>
    SetParameterList()
  	(
        ","
  		SetParameterList()
  	)*
    <KEYWORD_ON>
	(
        <IDENTIFIER>
	    |
	    ConstantInteger()
	    |
	    ConstantString()
	)
	(
	    ","
	    (
		    <IDENTIFIER>
			|
			ConstantInteger()
			|
			ConstantString()
		)
	)*
}

void SetParameterList():
{}
{
	Identifier()
	"="
	Constant()
}

void CreationStatement() :
{}
{
	<KEYWORD_CREATE>
	<KEYWORD_STREAM>
	Identifier() // StreamName
	FieldDefinitionList()
	[
	    <KEYWORD_AS>
	    StreamSelectionStatement()
	]
}

void FieldDefinitionList() :
{}
{
	"("
	FieldDefinition()
	(
		","
		FieldDefinition()
	)*
	")"
}

void FieldDefinition() :
{}
{
	Identifier() // DataStructureField
	FieldType()
	[
		<KEYWORD_DEFAULT>
		SignedConstant()
	]
}

void FieldList() :
{}
{
	"("
	Identifier() // DataStructureField
	(
		","
		Identifier() // DataStructureField
	)*
	")"
}

void InsertionStatement() :
{}
{
	<KEYWORD_INSERT>
	<KEYWORD_INTO>
	<KEYWORD_STREAM>
	Identifier() // StreamName
	[
	    FieldList()
	]
	StreamSelectionStatement()
}

void StreamSelectionStatement() :
{}
{
    LowEveryClause()
    LowSelectionStatement()
}

void LowEveryClause() :
{}
{
	<KEYWORD_EVERY>
	(
		LOOKAHEAD(2)
		(
			Duration()
			[
				<KEYWORD_SYNCHRONIZED>
			]
		)
		|
		(
			SamplesNumber()
		)
	)
}

void LowSelectionStatement() :
{}
{
	SelectClause()
	[
		GroupByClause()
	]
	[
		HavingClause()
	]
	[
		UpToClause()
	]
	[
		OnEmptySelectionClause()
	]
	SamplingClause()
	[
		ExecuteIfClause()
	]
	[
		TerminateAfterClause()
	]
}

void SelectClause() :
{}
{
	<KEYWORD_SELECT>
	[
		<KEYWORD_DISTINCT>
		|
		<KEYWORD_ALL>
	]
	FieldSelectionList()
}

void FieldSelectionList() :
{}
{
	FieldSelection()
	(
		","
		FieldSelection()
	)*
}

void FieldSelection() :
{}
{
	Expression(true)
	[
		<KEYWORD_DEFAULT>
		SignedConstant()
	]
}

void GroupByClause() :
{}
{
	<KEYWORD_GROUP>
	<KEYWORD_BY>
	FieldGroupingByList()
}

void FieldGroupingByList() :
{}
{
	(
		FieldGroupingBy()
		|
		FieldGroupingByTs()
	)
	(
		","
		FieldGroupingBy()
	)*
}

void FieldGroupingBy() :
{}
{
	Identifier() // Logical object field
}

void FieldGroupingByTs() :
{}
{
	<TYPE_TS>
	"("
	Duration()
	","
	ConstantInteger()
	<KEYWORD_GROUPS>
	")"
}

void HavingClause() :
{}
{
	<KEYWORD_HAVING>
	Expression(true)
}

void UpToClause() :
{}
{
	<KEYWORD_UP>
	<KEYWORD_TO>
	WindowSize()
}

void OnEmptySelectionClause() :
{}
{
	<KEYWORD_ON>
	<KEYWORD_EMPTY>
	<KEYWORD_SELECTION>
	<KEYWORD_INSERT>
	(
		<KEYWORD_NOTHING>
		|
	 	<KEYWORD_DEFAULT>
	)
}

void SamplingClause() :
{}
{
	<KEYWORD_SAMPLING>
	(
		OnEventClause()
		|
		(
			SamplingIfEveryClause()
			[
				OnUnsupportedSRClause()
			]
			[
				RefreshClause()
			]
		)
	)
	[
	    <KEYWORD_WHERE>
	    Expression(false) // No aggregates allowed
	]
}

void OnEventClause() :
{}
{
	<KEYWORD_ON>
	<KEYWORD_EVENT>
	EventList()
}

void EventList() :
{}
{
	Identifier() // LogicalObjectEvent
	(
		","
		Identifier() // LogicalObjectEvent
	)*
}

void SamplingIfEveryClause() :
{}
{
	(
		(
			SamplingIfClause()
			SamplingEveryClause()
		)+
		(
			<KEYWORD_ELSE>
			SamplingEveryClause()
		)
	)
	|
	(
		SamplingEveryClause()
	)
}

void SamplingIfClause() :
{}
{
	<KEYWORD_IF>
	Expression(false) // No aggregations allowed
}

void SamplingEveryClause() :
{}
{
	<KEYWORD_EVERY>
	Expression(false) // No aggregations allowed
	TimeUnit()
}

void OnUnsupportedSRClause() :
{}
{
	<KEYWORD_ON>
	<KEYWORD_UNSUPPORTED>
	<KEYWORD_SAMPLE>
	<KEYWORD_RATE>
	(
		<KEYWORD_DO>
		<OPERATOR_NOT>
		<KEYWORD_SAMPLE>
	)
	|
	(
		<KEYWORD_SLOW>
		<KEYWORD_DOWN>
	)
}

void RefreshClause() :
{}
{
	<KEYWORD_REFRESH>
	(
		(
			OnEventClause()
		)
		|
		(
			<KEYWORD_EVERY>
			Duration()
		)
		|
		(
			<KEYWORD_NEVER>
		)
	)
}

void ExecuteIfClause() :
{}
{
	<KEYWORD_EXECUTE>
	<KEYWORD_IF>
	Expression(false) // No aggregations allowed
	[
		RefreshClause()
	]
}

void TerminateAfterClause() :
{}
{
	<KEYWORD_TERMINATE>
	<KEYWORD_AFTER>
	(
		LOOKAHEAD(2)
		Duration()
		|
		SelectionsNumber()
	)
}

/*
 * SIGNS, CONSTANTS, VALUES, ETC.
 */
void Sign() :
{}
{
	<OPERATOR_PLUS>
	|
	<OPERATOR_MINUS>
}

void LogicValue() :
{}
{
	<CONSTANT_BOOLEAN_TRUE>
	|
	<CONSTANT_BOOLEAN_FALSE>
	|
	<CONSTANT_BOOLEAN_UNKNOWN>
}

void ConstantNull() :
{}
{
	<CONSTANT_NULL>
}

void ConstantBoolean() :
{}
{
	(
		<CONSTANT_BOOLEAN_TRUE>
		|
		<CONSTANT_BOOLEAN_FALSE>
	)
}

void ConstantFloat() :
{}
{
	(
		<CONSTANT_INTEGER>
		|
		<CONSTANT_FLOAT>
	)
}

void ConstantInteger() :
{}
{
	<CONSTANT_INTEGER>
}

void ConstantString() :
{}
{
	(
		(
		<CONSTANT_SINGLE_QUOTED_STRING_START>
		<CONSTANT_SINGLE_QUOTED_STRING_VALUE>
		<CONSTANT_SINGLE_QUOTED_STRING_END>
		)
		|
		(
		<CONSTANT_DOUBLE_QUOTED_STRING_START>
		<CONSTANT_DOUBLE_QUOTED_STRING_VALUE>
		<CONSTANT_DOUBLE_QUOTED_STRING_END>
		)
	)
}

void Constant() :
{}
{
	(
		ConstantNull()
		|
		ConstantBoolean()
		|
		ConstantString()
		|
		LOOKAHEAD(1)
		ConstantInteger()
		|
		ConstantFloat()
	)
}

void SignedConstant() :
{}
{
	[
		Sign()
	]
	Constant()
}

void ComparisonOperator() :
{}
{
	<OPERATOR_GREATER>
	|
	<OPERATOR_LESS>
	|
	<OPERATOR_GREATER_EQUAL>
	|
	<OPERATOR_LESS_EQUAL>
	|
	<OPERATOR_EQUAL>
	|
	<OPERATOR_NOT_EQUAL>
}

void FieldType() :
{}
{
	(
	<TYPE_ID>
	|
	<TYPE_TS>
	|
	<TYPE_BOOLEAN>
	|
	<TYPE_INTEGER>
	|
	<TYPE_FLOAT>
	|
	<TYPE_STRING>
	|
	<IDENTIFIER>
	)
}

void TimeUnit() :
{}
{
	(
	<TIMEUNIT_S>
	|
	<TIMEUNIT_M>
	|
	<TIMEUNIT_H>
	|
	<TIMEUNIT_MS>
	|
	<TIMEUNIT_D>
	|
	<TIMEUNIT_MT>
	)
}

void AggregationOperator() :
{}
{
	<FUNCTION_AVG>
	|
	<FUNCTION_MIN>
	|
	<FUNCTION_MAX>
	|
	<FUNCTION_SUM>
}

void Duration() :
{}
{
	ConstantFloat()
	TimeUnit()
}

void SamplesNumber() :
{}
{
	(
		(
			ConstantInteger()
			<KEYWORD_SAMPLES>
		)
		|
		(
			<KEYWORD_ONE>
		)
	)
}

void SelectionsNumber() :
{}
{
	(
		ConstantInteger()
		<KEYWORD_SELECTIONS>
	)
}

void WindowSize() :
{}
{
	LOOKAHEAD(2)
	Duration()
	|
	SamplesNumber()
}

void Identifier() :
{}
{
	<IDENTIFIER>
}

void ExistsAttribute() :
{}
{
	<KEYWORD_EXISTS>
	"("
	(
		Identifier() // LogicalObjectField
		|
		<KEYWORD_ALL>
	)
	")"
}

/*
 * EXPRESSIONS
 */
void Expression(boolean aggregate) :
{}
{
	ExpressionBooleanTerm(aggregate)
	(
		<OPERATOR_OR>
		ExpressionBooleanTerm(aggregate)
	)*
}

void ExpressionBooleanTerm(boolean aggregate) :
{}
{
	ExpressionBooleanFactor(aggregate)
	(
		<OPERATOR_AND>
		ExpressionBooleanFactor(aggregate)
	)*
}

void ExpressionBooleanFactor(boolean aggregate) :
{}
{
	ExpressionBooleanTest(aggregate)
	(
		<OPERATOR_XOR>
		ExpressionBooleanTest(aggregate)
	)*
}

void ExpressionBooleanTest(boolean aggregate) :
{}
{
	(
		<OPERATOR_NOT>
	)*
	ExpressionBooleanPrimary(aggregate)
}

void ExpressionBooleanPrimary(boolean aggregate) :
{}
{
	ExpressionBit(aggregate)
	[
		(
			ComparisonOperator()
			ExpressionBit(aggregate)
			[
				LogicTest()
			]
		)
		|
		(
			<KEYWORD_IS>
			[
				<OPERATOR_NOT>
			]
			(
				(
					<CONSTANT_NULL>
					[
						LogicTest()
					]
				)
				|
				(
					LogicValue()
				)
			)
		)
		|
		(
			<KEYWORD_BETWEEN>
			ExpressionBit(aggregate)
			<OPERATOR_AND>
			ExpressionBit(aggregate)
			[
				LogicTest()
			]
		)
		|
		(
			<KEYWORD_LIKE>
			ConstantString()
			[
				LogicTest()
			]
		)
	]
}

void LogicTest() :
{}
{
	<KEYWORD_IS>
	[
		<OPERATOR_NOT>
	]
	LogicValue()
}

void ExpressionBit(boolean aggregate) :
{}
{
	ExpressionBitTerm(aggregate)
	(
		<OPERATOR_BITWISE_OR>
		ExpressionBitTerm(aggregate)
	)*
}

void ExpressionBitTerm(boolean aggregate) :
{}
{
	ExpressionBitFactor(aggregate)
	(
		<OPERATOR_BITWISE_AND>
		ExpressionBitFactor(aggregate)
	)*
}

void ExpressionBitFactor(boolean aggregate) :
{}
{
	ExpressionBitShift(aggregate)
	(
		<OPERATOR_BITWISE_XOR>
		ExpressionBitShift(aggregate)
	)*
}

void ExpressionBitShift(boolean aggregate) :
{}
{
	ExpressionBitTest(aggregate)
	[
		(
			<OPERATOR_BITWISE_LSH>
			|
			<OPERATOR_BITWISE_RSH>
		)
		ConstantInteger()
	]
}

void ExpressionBitTest(boolean aggregate) :
{}
{
	(
		<OPERATOR_BITWISE_NOT>
	)*
	ExpressionValue(aggregate)
}

void ExpressionValue(boolean aggregate) :
{}
{
	ExpressionValueTerm(aggregate)
	(
		(
			<OPERATOR_PLUS>
			|
			<OPERATOR_MINUS>
		)
		ExpressionValueTerm(aggregate)
	)*
}

void ExpressionValueTerm(boolean aggregate) :
{}
{
	ExpressionValueFactor(aggregate)
	(
		(
			<OPERATOR_MULTIPLY>
			|
			<OPERATOR_DIVIDE>
		)
		ExpressionValueFactor(aggregate)
	)*
}

void ExpressionValueFactor(boolean aggregate) :
{}
{
	[
		Sign()
	]
	ExpressionValuePrimary(aggregate)
}

void ExpressionValuePrimary(boolean aggregate) :
{}
{
	Constant()
	|
	ExistsAttribute()
	|
	(
		"("
		Expression(aggregate)
		")"
	)
	|
	LOOKAHEAD( { aggregate } )
	(
		<TYPE_ID>
		|
		<KEYWORD_GROUPTS>
		|
		Aggregate(aggregate)
		|
		Identifier() // LogicalObjectField
	)
	|
	LOOKAHEAD( { !aggregate } )
	(
		<TYPE_ID>
		|
		Identifier() // LogicalObjectField
	)
}

void Aggregate(boolean aggregate) :
{}
{
    (
        <FUNCTION_COUNT>
        "("
        "*"
        ","
        WindowSize()
        [
            ","
            Expression(aggregate)
        ]
        ")"
    )
    |
    (
        AggregationOperator()
        "("
        Expression(aggregate)
        ","
        WindowSize()
        [
            ","
            Expression(aggregate)
        ]
        ")"
    )
}

