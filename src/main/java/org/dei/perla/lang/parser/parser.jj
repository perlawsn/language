/* Copyright (C) 2008  Romolo Camplani, Marco Fortunato, Marco Marelli, Guido Rota, Fabio A. Schreiber et al.
 *
 * This program is free software; you can redistribute it and/or
 * modify it under the terms of the GNU General Public License
 * as published by the Free Software Foundation; either version 2
 * of the License, or (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA  02110-1301, USA.
 */
options
{
    JDK_VERSION = "1.8";
    IGNORE_CASE = true;
    STATIC = false;
}

PARSER_BEGIN(Parser)

    package org.dei.perla.lang.parser;

    import java.time.temporal.TemporalUnit;
    import java.time.temporal.ChronoUnit;
    import java.time.Duration;
    import java.util.List;
    import java.util.ArrayList;

    import org.dei.perla.lang.executor.statement.WindowSize;
    import org.dei.perla.lang.executor.expression.*;
    import org.dei.perla.lang.executor.statement.*;

    import org.dei.perla.core.utils.Errors;
    import org.dei.perla.core.descriptor.DataType;

	public class Parser {

	    private static final void addError(String msg, Token t, Errors err) {
	        err.addError("Line " + t.beginLine + ", Column " + t.beginColumn + ": " + msg);
	    }

	    private static final void addIfError(Expression e, Token t, Errors err) {
	        if (!(e instanceof ErrorExpression)) {
	            return;
	        }
	        addError(((ErrorExpression) e).getError(), t, err);
	    }

	}

PARSER_END(Parser)

/*
 * SEPARATORS
 */
SKIP :
{
 	" "
    |
	"\r"
    |
	"\t"
    |
	"\n"
}

/*
 *	COMMENTS
 */
MORE:
{
	"/*" : COMMENT
}

<COMMENT> MORE:
{
	<~[]>
}

<COMMENT> SKIP:
{
	"*/" : DEFAULT
}

/*
 * CREATE CLAUSE
 */
TOKEN :
{
	< KEYWORD_CREATE:   "CREATE" >
    |
	< KEYWORD_AS:       "AS"     >
}

/*
 * INSERT CLAUSE
 */
TOKEN :
{
	< KEYWORD_INSERT:   "INSERT" >
    |
	< KEYWORD_INTO:     "INTO" >
}

/*
 * EVERY CLAUSE
 */
TOKEN :
{
	< KEYWORD_EVERY:        "EVERY"        >
	|
	< KEYWORD_IN:           "IN"           >
}

/*
 * SELECT CLAUSE
 */
TOKEN :
{
	< KEYWORD_SELECT:    "SELECT"   >
    |
	// Virtual record field created by the GROUP BY clause
	<KEYWORD_GROUPTS:    "GROUP_TS" >
}

/*
 * WHERE CLAUSE
 */
TOKEN :
{
	< KEYWORD_WHERE:     "WHERE" >
}

/*
 * GROUP BY CLAUSE
 */
TOKEN :
{
	< KEYWORD_GROUP:     "GROUP"  >
    |
	< KEYWORD_BY:        "BY"     >
    |
    // Keyword employed to define the number of groups created by a timestamp
    // aggregation
	< KEYWORD_GROUPS:    "GROUPS" >
}

/*
 * HAVING CLAUSE
 */
TOKEN :
{
	< KEYWORD_HAVING:    "HAVING" >
}

/*
 * UP TO CLAUSE
 */
TOKEN :
{
	< KEYWORD_UP:        "UP" >
    |
	< KEYWORD_TO:        "TO" >
}

/*
 * ON EMPTY SELECTION CLAUSE
 */
TOKEN :
{
	< KEYWORD_ON:        "ON"        >
    |
	< KEYWORD_EMPTY:     "EMPTY"     >
    |
	< KEYWORD_SELECTION: "SELECTION" >
    |
//	< KEYWORD_INSERT:    "INSERT"    >
//  |
	< KEYWORD_NOTHING:   "NOTHING"   >
    |
	< KEYWORD_DEFAULT:   "DEFAULT"   >
}

/*
 * SAMPLING CLAUSE
 */
TOKEN :
{
	< KEYWORD_SAMPLING:  "SAMPLING" >
    |
//	< KEYWORD_ON:        "ON"       >
//  |
	< KEYWORD_EVENT:     "EVENT"    >
    |
//	< KEYWORD_IF:        "IF"       >
//  |
//	< KEYWORD_EVERY:     "EVERY"    >
//  |
	< KEYWORD_ELSE:      "ELSE"     >
}

/*
 * ON UNSUPPORTED SAMPLING RATE
 */
TOKEN :
{
//	< KEYWORD_ON:          "ON"          >
//  |
	< KEYWORD_UNSUPPORTED: "UNSUPPORTED" >
    |
	< KEYWORD_SAMPLE:      "SAMPLE"      >
    |
	< KEYWORD_RATE:        "RATE"        >
    |
	< KEYWORD_DO:          "DO"          >
    |
//	< KEYWORD_NOT:         "NOT"         >
//  |
	< KEYWORD_SLOW:        "SLOW"        >
    |
	< KEYWORD_DOWN:        "DOWN"        >
}

/*
 * REFRESH CLAUSE
 */
TOKEN :
{
	< KEYWORD_REFRESH:   "REFRESH" >
    |
//	< KEYWORD_EVERY:     "EVERY"   >
//  |
	< KEYWORD_NEVER:     "NEVER"   >
}

/*
 * EXECUTE IF CLAUSE
 */
TOKEN :
{
	< KEYWORD_EXECUTE:   "EXECUTE" >
    |
	< KEYWORD_IF:        "IF"      >
    |
	< KEYWORD_EXISTS:    "EXISTS"  >
	|
	< KEYWORD_ALL:       "ALL"     >
}

/*
 * TERMINATE AFTER CLAUSE
 */
TOKEN :
{
	< KEYWORD_TERMINATE:  "TERMINATE"  >
    |
	< KEYWORD_AFTER:      "AFTER"      >
    |
	< KEYWORD_SELECTIONS: "SELECTIONS" >
}

/*
 * FROM CLAUSE
 */
TOKEN :
{
	< KEYWORD_FROM:      "FROM" >
//  |
//	< KEYWORD_AS:        "AS"   >
}

/*
 * SET CLAUSE
 */
TOKEN :
{
  	< KEYWORD_SET:    "SET"  >
//  |
//  < KEYWORD_ON: "ON"    >
}

/*
 * STREAM DATA STRUCTURE
 */
TOKEN :
{
	< KEYWORD_STREAM:   "STREAM"   >
}

/*
 * MISC TOKENS
 */
TOKEN :
{
	< KEYWORD_SAMPLES:  "SAMPLES"  >
    |
	< KEYWORD_ONE:      "ONE"      >
    |
	< KEYWORD_IS:       "IS" >
    |
	< KEYWORD_BETWEEN:  "BETWEEN"  >
    |
	< KEYWORD_LIKE:     "LIKE"     >
}

/*
 * ALGEBRAIC OPERATORS
 */
TOKEN :
{
	< OPERATOR_MULTIPLY: "*" >
    |
	< OPERATOR_DIVIDE:   "/" >
	|
	<OPERATOR_MODULO:    "%" >
    |
	< OPERATOR_PLUS:     "+" >
    |
	< OPERATOR_MINUS:    "-" >
}

/*
 * BOOLEAN OPERATORS
 */
TOKEN :
{
	< OPERATOR_NOT:     "NOT" >
    |
	< OPERATOR_XOR:     "XOR" >
    |
	< OPERATOR_AND:     "AND" >
    |
    < OPERATOR_OR:      "OR"  >
}

/*
 * BITWISE OPERATORS
 */
TOKEN :
{
	< OPERATOR_BITWISE_NOT: "~" >
    |
	< OPERATOR_BITWISE_LSH: "<<" >
    |
	< OPERATOR_BITWISE_RSH: ">>" >
    |
    < OPERATOR_BITWISE_XOR: "^" >
    |
	< OPERATOR_BITWISE_AND: "&" >
    |
	< OPERATOR_BITWISE_OR:  "|"  >
}

/*
 * COMPARISON OPERATORS
 */
TOKEN :
{
	< OPERATOR_GREATER:       ">"  >
    |
	< OPERATOR_LESS:          "<"  >
    |
	< OPERATOR_GREATER_EQUAL: ">=" >
    |
	< OPERATOR_LESS_EQUAL:    "<=" >
    |
	< OPERATOR_EQUAL:         "="  >
    |
	< OPERATOR_NOT_EQUAL:     "<>" | "!=" >
}

/*
 * AGGREGATES
 */
TOKEN :
{
	< FUNCTION_COUNT:     "COUNT"     >
    |
	< FUNCTION_AVG:       "AVG"       >
    |
	< FUNCTION_MAX:       "MAX"       >
    |
	< FUNCTION_MIN:       "MIN"       >
    |
	< FUNCTION_SUM:       "SUM"       >
//  |
//	< FUNCTION_TIMESTAMP: "TIMESTAMP" >
}

/*
 * TIME UNITS
 */
TOKEN :
{
	< TIMEUNIT_S:  "seconds"      | "s"  >
    |
	< TIMEUNIT_M:  "minutes"      | "m"  >
    |
	< TIMEUNIT_H:  "hours"        | "h"  >
    |
	< TIMEUNIT_MS: "milliseconds" | "ms" >
    |
	< TIMEUNIT_D:  "days"         | "d"  >
}

/*
 * DATA TYPES
 */
TOKEN :
{
	< TYPE_ID:      "ID"        >
    |
	< TYPE_TS:      "TIMESTAMP" >
    |
	< TYPE_BOOLEAN: "BOOLEAN"   >
    |
	< TYPE_INTEGER: "INTEGER"   >
    |
	< TYPE_FLOAT:   "FLOAT"     >
    |
	< TYPE_STRING:  "STRING"    >
}

/*
 * COSTANTS
 * Don't use these tokens, use the corresponding productions instead.
 */

/*
 * CONSTANT NULL
 */
TOKEN :
{
	< CONSTANT_NULL:     "NULL" >
}

/*
 * 3-VALUE BOOLEAN LOGIC (true, false, unknown)
 */
TOKEN :
{
	< CONSTANT_BOOLEAN_TRUE:    "TRUE"    >
    |
	< CONSTANT_BOOLEAN_FALSE:   "FALSE"   >
    |
	< CONSTANT_BOOLEAN_UNKNOWN: "UNKNOWN" >
}

/*
 * NUMERIC CONSTANTS (float and integer)
 */
TOKEN :
{
	<CONSTANT_INTEGER_10: ( <DIGIT> )+ >
	|
	<CONSTANT_INTEGER_16: ( <HEXADECIMAL> ) >
    |
	<CONSTANT_FLOAT:
        ( <CONSTANT_INTEGER_10> )? "." <CONSTANT_INTEGER_10>
	>
}

/*
 * STRING CONSTANTS
 * Strings can be single quoted or double quoted. The quote character is
 * escaped when doubled (e.g., 'test '' string' or "test "" string"
 */
TOKEN :
{
	< CONSTANT_SINGLE_QUOTED_STRING_START: "'"  > : NON_SINGLE_QUOTED_STRING
    |
	< CONSTANT_DOUBLE_QUOTED_STRING_START: "\"" > : NON_DOUBLE_QUOTED_STRING
}

<NON_SINGLE_QUOTED_STRING> TOKEN :
{
	< CONSTANT_SINGLE_QUOTED_STRING_VALUE: (~["'"] | "'" "'")* > : NON_SINGLE_QUOTED_STRING_END
}

<NON_DOUBLE_QUOTED_STRING> TOKEN :
{
	< CONSTANT_DOUBLE_QUOTED_STRING_VALUE: (~["\""] | "\"" "\"")* > : NON_DOUBLE_QUOTED_STRING_END
}

<NON_SINGLE_QUOTED_STRING_END> TOKEN :
{
	< CONSTANT_SINGLE_QUOTED_STRING_END:  "'" > : DEFAULT
}

<NON_DOUBLE_QUOTED_STRING_END> TOKEN :
{
	< CONSTANT_DOUBLE_QUOTED_STRING_END: "\"" > : DEFAULT
}

/*
 * IDENTIFIERS
 */
TOKEN :
{
	< #DIGIT:      ["0" - "9"]               >
    |
	< #LITERAL:    ["a" - "z", "A" - "Z"] >
    |
	< #UNDERSCORE: "_"                       >
    |
	< #HEXADECIMAL: "0" ["x","X"] (["0"-"9","a"-"f","A"-"F"])+ >
    |
	< IDENTIFIER: ( <LITERAL> | <UNDERSCORE>) ( <DIGIT> | <LITERAL> | <UNDERSCORE> )* >
}

/*
 * PRODUCTIONS
 */

List<Statement> StatementSequence(Errors err) :
{
    List<Statement> sl = new ArrayList<Statement>();
    Statement s;
}
{
	s = Statement(err)
	{ sl.add(s); }
	(
		";"
		s = Statement(err)
		{ sl.add(s); }
	)*
	<EOF>
	{ return sl; }
}

Statement Statement(Errors err):
{
    Statement s;
}
{
    (
    s = SetParametersStament(err)
    |
    s = CreationStatement(err)
    |
    s = InsertionStatement(err)
    )
    { return s; }
}

Statement SetParametersStament(Errors err):
{}
{
    <KEYWORD_SET>
    SetParameterList(err)
  	(
        ","
  		SetParameterList(err)
  	)*
    <KEYWORD_ON>
	(
        <IDENTIFIER>
	    |
	    ConstantInteger()
	    |
	    ConstantString()
	)
	(
	    ","
	    (
		    <IDENTIFIER>
			|
			ConstantInteger()
			|
			ConstantString()
		)
	)*
	{ return null; }
}

void SetParameterList(Errors err):
{}
{
	Identifier()
	"="
	Expression(false, err)
}

Statement CreationStatement(Errors err) :
{}
{
	<KEYWORD_CREATE>
	<KEYWORD_STREAM>
	Identifier() // StreamName
	FieldDefinitionList(err)
	[
	    <KEYWORD_AS>
	    SelectionStatement(err)
	]
	{ return null; }
}

void FieldDefinitionList(Errors err) :
{}
{
	"("
	FieldDefinition(err)
	(
		","
		FieldDefinition(err)
	)*
	")"
}

void FieldDefinition(Errors err) :
{}
{
	Identifier() // DataStructureField
	FieldType()
	[
		<KEYWORD_DEFAULT>
		Expression(false, err)
	]
}

Statement InsertionStatement(Errors err) :
{
    Selection sel;
}
{
	<KEYWORD_INSERT>
	<KEYWORD_INTO>
	<KEYWORD_STREAM>
	Identifier() // StreamName
	[
	    FieldList()
	]
	sel = SelectionStatement(err)
	{ return null; }
}

List<String> FieldList() :
{
    List<String> sl = new ArrayList<String>();
    String s;
}
{
	"("
	s = Identifier() // DataStructureField
	{ sl.add(s); }
	(
		","
		s = Identifier() // DataStructureField
		{ sl.add(s); }
	)*
	")"
	{ return sl; }
}

Selection SelectionStatement(Errors err) :
{
    WindowSize every;
    List<FieldSelection> fsl;
    GroupBy gby = null;
    Expression having = null;
    WindowSize upto = WindowSize.ONE;
    OnEmptySelection oes = OnEmptySelection.INSERT_NOTHING;

    Select sel;
}
{
    every = EveryClause(err)
    <KEYWORD_SELECT>
    fsl = FieldSelectionList(err)
	[
		gby = GroupByClause()
	]
	[
		having = HavingClause(err)
	]
	[
		upto = UpToClause(err)
	]
	[
		oes = OnEmptySelectionClause()
	]
	{
	    List<Expression> fields = new ArrayList<Expression>();
	    Object[] def = new Object[fsl.size()];
	    int i = 0;
	    for (FieldSelection fs : fsl) {
	        fields.add(fs.getField());
	        def[i++] = fs.getDefault();
	    }
	    if (oes == OnEmptySelection.INSERT_NOTHING) {
	        def = null;
	    }
	    sel = new Select(fields, upto, gby, having, def);
	}
	SamplingClause(err)
	[
		ExecuteIfClause(err)
	]
	[
		TerminateAfterClause(err)
	]
	{ return null; }
}

WindowSize EveryClause(Errors err) :
{
    WindowSize ws;
}
{
	<KEYWORD_EVERY>
	ws = WindowSize(err)
	{ return ws; }
}

List<FieldSelection> FieldSelectionList(Errors err) :
{
    List<FieldSelection> fsl = new ArrayList<FieldSelection>();
    FieldSelection fs;
}
{
	fs = FieldSelection(err)
	{ fsl.add(fs); }
	(
		","
		fs = FieldSelection(err)
		{ fsl.add(fs); }
	)*
	{ return fsl; }
}

FieldSelection FieldSelection(Errors err) :
{
    Expression field;
    Object def = null;
}
{
	field = Expression(true, err)
	[
		<KEYWORD_DEFAULT>
		(
            def = Expression(false, err)
            |
            <CONSTANT_NULL>
		)
	]
	{ return new FieldSelection(field, def); }
}

GroupBy GroupByClause() :
{
    GroupBy g;
}
{
	<KEYWORD_GROUP>
	<KEYWORD_BY>
	g = FieldGroupingByList()
	{ return g; }
}

GroupBy FieldGroupingByList() :
{
    GroupByTS gts = null;
    Field f;
    List<Field> fs = new ArrayList<Field>();
}
{
	(
		f = FieldGroupingBy()
		{ fs.add(f); }
		|
		gts = FieldGroupingByTs()
	)
	(
		","
		f = FieldGroupingBy()
		{ fs.add(f); }
	)*
	{
	    if (gts == null) {
	        return new GroupBy(fs);
	    }
	    return new GroupBy(gts.getDuration(), gts.getCount(), fs);
	}
}

Field FieldGroupingBy() :
{
    String f;
}
{
	f = Identifier() // FPC attribute
	{ return new Field(f); }
}

GroupByTS FieldGroupingByTs() :
{
    Duration d;
    int count;
}
{
	<TYPE_TS>
	"("
	d = Duration()
	","
	count = ConstantInteger()
	<KEYWORD_GROUPS>
	")"
	{ return new GroupByTS(d, count); }
}

Expression HavingClause(Errors err) :
{
    Expression e;
}
{
	<KEYWORD_HAVING>
	e = Expression(true, err)
	{ return e; }
}

WindowSize UpToClause(Errors err) :
{
    WindowSize ws;
}
{
	<KEYWORD_UP>
	<KEYWORD_TO>
	ws = WindowSize(err)
	{ return ws; }
}

OnEmptySelection OnEmptySelectionClause() :
{}
{
	<KEYWORD_ON>
	<KEYWORD_EMPTY>
	<KEYWORD_SELECTION>
	<KEYWORD_INSERT>
	(
		<KEYWORD_NOTHING>
		{ return OnEmptySelection.INSERT_NOTHING; }
		|
	 	<KEYWORD_DEFAULT>
	 	{ return OnEmptySelection.INSERT_DEFAULT; }
	)
}

void SamplingClause(Errors err) :
{}
{
	<KEYWORD_SAMPLING>
	(
		OnEventClause()
		|
		(
			SamplingIfEveryClause(err)
			[
				OnUnsupportedSRClause()
			]
			[
				RefreshClause()
			]
		)
	)
	[
	    <KEYWORD_WHERE>
	    Expression(false, err) // No aggregates allowed
	]
}

List<String> OnEventClause() :
{
    List<String> evs;
}
{
	<KEYWORD_ON>
	<KEYWORD_EVENT>
	evs = EventList()
	{ return evs; }
}

List<String> EventList() :
{
    String e;
    List<String> evs = new ArrayList<String>();
}
{
	e = Identifier() // FPC Event
	{ evs.add(e); }
	(
		","
		e = Identifier() // FPC Event
		{ evs.add(e); }
	)*
	{ return evs; }
}

void SamplingIfEveryClause(Errors err) :
{}
{
	(
		(
			SamplingIfClause(err)
			SamplingEveryClause(err)
		)+
		(
			<KEYWORD_ELSE>
			SamplingEveryClause(err)
		)
	)
	|
	(
		SamplingEveryClause(err)
	)
}

void SamplingIfClause(Errors err) :
{}
{
	<KEYWORD_IF>
	Expression(false, err) // No aggregations allowed
}

void SamplingEveryClause(Errors err) :
{}
{
	<KEYWORD_EVERY>
	Expression(false, err) // No aggregations allowed
	TimeUnit()
}

UnsupportedSamplingRate OnUnsupportedSRClause() :
{}
{
	<KEYWORD_ON>
	<KEYWORD_UNSUPPORTED>
	<KEYWORD_SAMPLE>
	<KEYWORD_RATE>
	(
		<KEYWORD_DO>
		<OPERATOR_NOT>
		<KEYWORD_SAMPLE>
		{ return UnsupportedSamplingRate.DO_NOT_SAMPLE; }
	)
	|
	(
		<KEYWORD_SLOW>
		<KEYWORD_DOWN>
		{ return UnsupportedSamplingRate.SLOW_DOWN; }
	)
}

void RefreshClause() :
{}
{
	<KEYWORD_REFRESH>
	(
		(
			OnEventClause()
		)
		|
		(
			<KEYWORD_EVERY>
			Duration()
		)
		|
		(
			<KEYWORD_NEVER>
		)
	)
}

void ExecuteIfClause(Errors err) :
{}
{
	<KEYWORD_EXECUTE>
	<KEYWORD_IF>
	Expression(false, err) // No aggregations allowed
	[
		RefreshClause()
	]
}

void TerminateAfterClause(Errors err) :
{}
{
	<KEYWORD_TERMINATE>
	<KEYWORD_AFTER>
	(
		LOOKAHEAD(2)
		Duration()
		|
		SelectionsNumber(err)
	)
}

/*
 * SIGNS, CONSTANTS, VALUES, ETC.
 */
Sign Sign() :
{}
{
	<OPERATOR_PLUS>
	{ return Sign.PLUS; }
	|
	<OPERATOR_MINUS>
	{ return Sign.MINUS; }
}

LogicValue ConstantBoolean() :
{}
{
	(
		<CONSTANT_BOOLEAN_TRUE>
		{ return LogicValue.TRUE; }
		|
		<CONSTANT_BOOLEAN_FALSE>
		{ return LogicValue.FALSE; }
	)
}

float ConstantFloat() :
{
    Token value;
}
{
	(
		value = <CONSTANT_FLOAT>
	)
	{ return Float.parseFloat(value.image); }
}

int ConstantInteger() :
{
    Token value;
}
{
	value = <CONSTANT_INTEGER_10>
	{ return Integer.parseInt(value.image); }
	|
	value = <CONSTANT_INTEGER_16>
	{ return Integer.parseInt(value.image.substring(2), 16); }
}

String ConstantString() :
{
    Token value;
}
{
	(
		(
		<CONSTANT_SINGLE_QUOTED_STRING_START>
		value = <CONSTANT_SINGLE_QUOTED_STRING_VALUE>
		<CONSTANT_SINGLE_QUOTED_STRING_END>
		)
		|
		(
		<CONSTANT_DOUBLE_QUOTED_STRING_START>
		value = <CONSTANT_DOUBLE_QUOTED_STRING_VALUE>
		<CONSTANT_DOUBLE_QUOTED_STRING_END>
		)
	)
	{ return value.image; }
}

Expression Constant() :
{
    Object value;
}
{
	(
		value = ConstantBoolean()
		{ return Constant.create(value, DataType.BOOLEAN); }
		|
		value = ConstantString()
		{ return Constant.create(value, DataType.STRING); }
		|
		LOOKAHEAD(1)
		value = ConstantInteger()
		{ return Constant.create(value, DataType.INTEGER); }
		|
		value = ConstantFloat()
		{ return Constant.create(value, DataType.FLOAT); }
	)
}

ComparisonOperation ComparisonOperation() :
{}
{
	<OPERATOR_GREATER>
	{ return ComparisonOperation.GT; }
	|
	<OPERATOR_LESS>
	{ return ComparisonOperation.LT; }
	|
	<OPERATOR_GREATER_EQUAL>
	{ return ComparisonOperation.GE; }
	|
	<OPERATOR_LESS_EQUAL>
	{ return ComparisonOperation.LE; }
	|
	<OPERATOR_EQUAL>
	{ return ComparisonOperation.EQ; }
	|
	<OPERATOR_NOT_EQUAL>
	{ return ComparisonOperation.NE; }
}

DataType FieldType() :
{}
{
	(
	<TYPE_ID>
	{ return DataType.ID; }
	|
	<TYPE_TS>
	{ return DataType.TIMESTAMP; }
	|
	<TYPE_BOOLEAN>
	{ return DataType.BOOLEAN; }
	|
	<TYPE_INTEGER>
	{ return DataType.INTEGER; }
	|
	<TYPE_FLOAT>
	{ return DataType.FLOAT; }
	|
	<TYPE_STRING>
	{ return DataType.STRING; }
	)
}

TemporalUnit TimeUnit() :
{}
{
	<TIMEUNIT_S>
	{ return ChronoUnit.SECONDS; }
	|
	<TIMEUNIT_M>
	{ return ChronoUnit.MINUTES; }
	|
	<TIMEUNIT_H>
	{ return ChronoUnit.HOURS; }
	|
	<TIMEUNIT_MS>
	{ return ChronoUnit.MILLIS; }
	|
	<TIMEUNIT_D>
	{ return ChronoUnit.DAYS; }
}

AggregateOperation AggregateOperation() :
{}
{
	<FUNCTION_AVG>
	{ return AggregateOperation.AVG; }
	|
	<FUNCTION_MIN>
	{ return AggregateOperation.MIN; }
	|
	<FUNCTION_MAX>
	{ return AggregateOperation.MAX; }
	|
	<FUNCTION_SUM>
	{ return AggregateOperation.SUM; }
}

Duration Duration() :
{
    TemporalUnit tu;
    int value;
}
{
	value = ConstantInteger()
	tu = TimeUnit()
    { return Duration.of(value, tu); }
}

int SamplesNumber() :
{
    int samples;
}
{
	(
		(
			samples = ConstantInteger()
			<KEYWORD_SAMPLES>
			{ return samples; }
		)
		|
		(
			<KEYWORD_ONE>
			{ return 1; }
		)
	)
}

int SelectionsNumber(Errors err) :
{

    Token t;
    int value;
}
{
	(
		value = ConstantInteger()
		{ t = token; }
		<KEYWORD_SELECTIONS>
		{
		    if (value == 0) {
		        addError("Number of selections must be greater than zero", t, err);
		    }
		    return value;
		}
	)
}

WindowSize WindowSize(Errors err) :
{
    Token t;
    Duration d;
    int samples;
    WindowSize ws;
}
{
    (
	    LOOKAHEAD(2)
	    d = Duration()
	    {
	        t = token;
	        ws = new WindowSize(d);
	    }
	    |
        samples = SamplesNumber()
        {
            t = token;
            ws = new WindowSize(samples);
        }
	)
	{
	    if (ws.isZero()) {
	        addError("Empty window size", t, err);
	    }
	    return ws;
	}
}

String Identifier() :
{
    Token value;
}
{
	value = <IDENTIFIER>
	{ return value.image; }
}

/*
 * EXPRESSIONS
 */
Expression Expression(boolean aggregate, Errors err) :
{
    Expression e1;
    Expression e2;
}
{
	e1 = BooleanTerm(aggregate, err)
	(
		<OPERATOR_OR>
		e2 = BooleanTerm(aggregate, err)
		{
		    e1 = Bool.createOR(e1, e2);
		    addIfError(e1, token, err);
		}
	)*
	{ return e1; }
}

Expression BooleanTerm(boolean aggregate, Errors err) :
{
    Expression e1;
    Expression e2;
}
{
	e1 = BooleanFactor(aggregate, err)
	(
		<OPERATOR_AND>
		e2 = BooleanFactor(aggregate, err)
		{
		    e1 = Bool.createAND(e1, e2);
		    addIfError(e1, token, err);
		}
	)*
	{ return e1; }
}

Expression BooleanFactor(boolean aggregate, Errors err) :
{
    Expression e1;
    Expression e2;
}
{
	e1 = BooleanNegation(aggregate, err)
	(
		<OPERATOR_XOR>
		e2 = BooleanNegation(aggregate, err)
		{
		    e1 = Bool.createXOR(e1, e2);
		    addIfError(e1, token, err);
		}
	)*
	{ return e1; }
}

Expression BooleanNegation(boolean aggregate, Errors err) :
{
    Expression e;
    boolean invert = false;
}
{
	(
		<OPERATOR_NOT>
		{ invert = true; }
	)*
	e = BooleanPredicate(aggregate, err)
	{
	    if (invert) {
	        return Bool.createNOT(e);
	        addIfError(e, token, err);
	    }
	    return e;
	}
}

Expression BooleanPredicate(boolean aggregate, Errors err) :
{
    Expression e;
}
{
    e = Comparison(aggregate, err)
    [
        e = Is(e, err)
        |
        e = Like(e, err)
        |
        e = Between(e, aggregate, err)
    ]
    { return e; }
}

Expression Is(Expression e, Errors err) :
{
    boolean invert = false;
    Expression is;
}
{
    <KEYWORD_IS>
    [
        <OPERATOR_NOT>
        { invert = true; }
    ]
    (
        <CONSTANT_BOOLEAN_TRUE>
        { is = Is.create(e, LogicValue.TRUE); }
        |
        <CONSTANT_BOOLEAN_FALSE>
        { is = Is.create(e, LogicValue.FALSE); }
        |
        <CONSTANT_BOOLEAN_UNKNOWN>
        { is = Is.create(e, LogicValue.UNKNOWN); }
        |
        <CONSTANT_NULL>
        { is = IsNull.create(e); }
    )
    {
        addIfError(is, token, err);
        if (invert) {
            is = Bool.createNOT(is);
            addIfError(is, token, err);
        }
        return is;
    }
}

Expression Like(Expression e, Errors err) :
{
    String pattern;
    Expression like;
}
{
    <KEYWORD_LIKE>
    pattern = ConstantString()
    {
        like = Like.create(e, pattern);
        addIfError(like, token, err);
        return like;
    }
}

Expression Between(Expression e, boolean aggregate, Errors err) :
{
    Expression b;
    Expression min;
    Expression max;
}
{
    <KEYWORD_BETWEEN>
    min = BitwiseExpression(aggregate, err)
    <OPERATOR_AND>
    max = BitwiseExpression(aggregate, err)
    {
        b = Between.create(e, min, max);
        addIfError(b, token, err);
        return b;
    }
}

Expression Comparison(boolean aggregate, Errors err) :
{
    Expression e1;
    Expression e2;
    ComparisonOperation op;
}
{
    e1 = BitwiseExpression(aggregate, err)
    [
        op = ComparisonOperation()
        e2 = BitwiseExpression(aggregate, err)
        {
            e1 = Comparison.create(op, e1, e2);
            addIfError(e1, token, err);
        }
    ]
    { return e1; }
}

Expression BitwiseExpression(boolean aggregate, Errors err) :
{
    Expression e1;
    Expression e2;
}
{
	e1 = BitwiseTerm(aggregate, err)
	(
		<OPERATOR_BITWISE_OR>
		e2 = BitwiseTerm(aggregate, err)
		{
		    e1 = Bitwise.createOR(e1, e2);
		    addIfError(e1, token, err);
		}
	)*
	{ return e1; }
}

Expression BitwiseTerm(boolean aggregate, Errors err) :
{
    Expression e1;
    Expression e2;
}
{
	e1 = BitwiseFactor(aggregate, err)
	(
		<OPERATOR_BITWISE_AND>
		e2 = BitwiseFactor(aggregate, err)
		{
		    e1 = Bitwise.createAND(e1, e2);
		    addIfError(e1, token, err);
		}
	)*
	{ return e1; }
}

Expression BitwiseFactor(boolean aggregate, Errors err) :
{
    Expression e1;
    Expression e2;
}
{
	e1 = BitwiseShift(aggregate, err)
	(
		<OPERATOR_BITWISE_XOR>
		e2 = BitwiseShift(aggregate, err)
		{
		    e1 = Bitwise.createXOR(e1, e2);
		    addIfError(e1, token, err);
		}
	)*
	{ return e1; }
}

Expression BitwiseShift(boolean aggregate, Errors err) :
{
    Expression e1;
    Expression e2;
    BitwiseOperation op;
}
{
	e1 = BitwiseNegation(aggregate, err)
	[
		(
			<OPERATOR_BITWISE_LSH>
			{ op = BitwiseOperation.LSH; }
			|
			<OPERATOR_BITWISE_RSH>
			{ op = BitwiseOperation.RSH; }
		)
		e2 = BitwiseNegation(aggregate, err)
		{
		    e1 = Bitwise.create(op, e1, e2);
		    addIfError(e1, token, err);
		}
	]
	{ return e1; }
}

Expression BitwiseNegation(boolean aggregate, Errors err) :
{
    Expression e;
    boolean not = false;
}
{
	(
		<OPERATOR_BITWISE_NOT>
		{ not = true; }
	)*
	e = ArithmeticExpression(aggregate, err)
	{
	    if (not) {
	        e = Bitwise.createNOT(e);
	        addIfError(e, token, err);
	    }
	    return e;
	}
}

Expression ArithmeticExpression(boolean aggregate, Errors err) :
{
    Expression e1;
    Expression e2;
    ArithmeticOperation op;
}
{
	e1 = ArithmeticTerm(aggregate, err)
	(
		(
			<OPERATOR_PLUS>
			{ op = ArithmeticOperation.ADDITION; }
			|
			<OPERATOR_MINUS>
			{ op = ArithmeticOperation.SUBTRACTION; }
		)
		e2 = ArithmeticTerm(aggregate, err)
		{
		    e1 = Arithmetic.create(op, e1, e2);
		    addIfError(e1, token, err);
		}
	)*
	{ return e1; }
}

Expression ArithmeticTerm(boolean aggregate, Errors err) :
{
    Expression e1;
    Expression e2;
    ArithmeticOperation op;
}
{
	e1 = ArithmeticFactor(aggregate, err)
	(
		(
			<OPERATOR_MULTIPLY>
			{ op = ArithmeticOperation.PRODUCT; }
			|
			<OPERATOR_DIVIDE>
			{ op = ArithmeticOperation.DIVISION; }
			|
			<OPERATOR_MODULO>
			{ op = ArithmeticOperation.MODULO; }
		)
		e2 = ArithmeticFactor(aggregate, err)
		{
		    e1 = Arithmetic.create(op, e1, e2);
		    addIfError(e1, token, err);
		}
	)*
	{ return e1; }
}

Expression ArithmeticFactor(boolean aggregate, Errors err) :
{
    Expression e;
    Sign s = Sign.PLUS;
}
{
	[
		s = Sign()
	]
	e = PrimaryExpression(aggregate, err)
	{
	    if (s == Sign.MINUS) {
	        e = Arithmetic.createInverse(e);
	        addIfError(e, token, err);
	    }
	    return e;
	}
}

Expression PrimaryExpression(boolean aggregate, Errors err) :
{
    String s;
    Expression e;
}
{
	e = Constant()
	{
	    addIfError(e, token, err);
	    return e;
	}
	|
	e = ExistsAttribute()
	{
	    addIfError(e, token, err);
	    return e;
	}
	|
	(
	"("
        e = Expression(aggregate, err)
        {
            addIfError(e, token, err);
            return e;
        }
        ")"
	)
	|
    <KEYWORD_GROUPTS>
    {
        if (!aggregate) {
            addError("aggregation not allowed", token, err);
            return new ErrorExpression("aggregation not allowed");
        }
        return new GroupTS();
    }
    |
    e = Aggregate(err)
    {
        if (!aggregate) {
            addError("aggregation not allowed", token, err);
            return new ErrorExpression("aggregation not allowed");
        }
        return e;
    }
    |
    s = Identifier() // FPC attribute
    { return new Field(s); }
}

Expression ExistsAttribute() :
{}
{
	<KEYWORD_EXISTS>
	"("
	    (
		Identifier() // FPC attribute
		|
		<KEYWORD_ALL>
	    )
	")"
	{ return null; }
}

Expression Aggregate(Errors err) :
{
    Token t;
    AggregateOperation op;
    Expression exp = null;
    WindowSize ws;
    Expression filter = null;
    Expression e;
}
{
    (
        (
        <FUNCTION_COUNT>
        { t = token; }
        { op = AggregateOperation.COUNT; }
        "("
        "*"
        ","
        ws = WindowSize(err)
        [
            ","
            filter = Expression(false, err)
        ]
        ")"
        )
        |
        (
        op = AggregateOperation()
        { t = token; }
        "("
        exp = Expression(false, err)
        ","
        ws = WindowSize(err)
        [
            ","
            filter = Expression(false, err)
        ]
        ")"
        )
    )
    {
        e = Aggregate.create(op, exp, ws, filter);
        addIfError(e, t, err);
        return e;
    }
}

